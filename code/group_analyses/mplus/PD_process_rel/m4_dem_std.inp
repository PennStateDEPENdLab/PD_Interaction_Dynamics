TITLE:
Aff -> Coreg -> Aff -> AVOAND fix non-sig parameters to zero
DATA:
FILE = "../df67_das_dem.dat";

VARIABLE:
NAMES =  PTNUM scpt ccpt scpr ccpr ECRanx_1 ECRanx_0 prnapt prnapr pnapt pnapr elpt
     elpr cmpt cmpr prafpt prafpr pafpt pafpr ECRavo_0 ECRavo_1 whichdf_num id rel
     dasfu1 dasfu0 dasbl1 dasbl0 age_0 age_1 r_0 r_1 female_0 female_1 R2_cut
     uni_cut;
 MISSING=.;
USEVARIABLES = cmpt cmpr scpt ccpt scpr ccpr prafpt prafpr pafpt pafpr
ECRavo_1 ECRavo_0 age_0 age_1 r_0 r_1 female_0 female_1;
IDVARIABLE IS id;
ANALYSIS:
ESTIMATOR=BAYES; PROCESSORS = 2; BITERATIONS = (49000); !ALGORITHM=MH;
!ESTIMATOR = MLR;
MODEL:

ccpr ON r_0;
ccpt ON r_1;
ccpr ON age_0;
ccpt ON age_1;
ccpr ON female_0;
ccpt ON female_1;

scpr ON r_0;
scpt ON r_1;
scpr ON age_0;
scpt ON age_1;
scpr ON female_0;
scpt ON female_1;
prafpt ON cmpt@0;
prafpr ON cmpr@0;
ccpt ON cmpt* (x);
scpt ON cmpt@0;
ccpr ON cmpr@0;
scpr ON cmpr@0;
cmpt WITH cmpt;
cmpr WITH cmpr;
cmpt WITH cmpr;
[cmpt];
[cmpr];
prafpt WITH prafpr;

ECRavo_1 ON pafpt* (k);
ECRavo_0 ON pafpr;
pafpt WITH pafpr;
pafpt ON prafpt;
pafpt ON scpt* (a1);
pafpt ON ccpt* (a3);
pafpt ON scpr* (a5);
pafpt ON ccpr* (a7);
pafpr ON prafpr;
pafpr ON scpr@0;
pafpr ON ccpr@0;
pafpr ON scpt* (a5);
pafpr ON ccpt* (a7);
scpr ON prafpt@0;
scpt ON prafpr@0;
ccpt ON prafpr* (b7);
ccpr ON prafpt* (b7);
scpt WITH ccpt;
scpt WITH scpr;
scpt WITH ccpr;
ccpt WITH scpr;
ccpt WITH ccpr;
scpr WITH ccpr;
scpt WITH scpt;
ccpt WITH ccpt;
scpr WITH scpr;
ccpr WITH ccpr;
prafpt WITH prafpt;
prafpr WITH prafpr;
pafpt WITH pafpt;
pafpr WITH pafpr;
[scpt];
[ccpt];
[scpr];
[ccpr];
[prafpt];
[prafpr];
[pafpt];
[pafpr@0];
ECRavo_1 WITH ECRavo_1;
ECRavo_0 WITH ECRavo_0;
ECRavo_1 WITH ECRavo_0@0;
[ECRavo_1];
[ECRavo_0];
!BASED ON MOD INDICES
ECRavo_0 ON prafpr;
ECRAVO_0 ON CMPR;
!MODEL CONSTRAINT:
!NEW ( a3b7 ka3 xka3); ! a7b7 a4b7 zka3 );
!ha3 = h*a3;
!xka3 = x*k*a3;
!ka3 = k*a3;
!a3b7 = a3*b7;
!a7b7 = a7*b7;
!a4b7 = a4*b7;
!zka3 = z*k*a3;

OUTPUT:
STANDARDIZED;!  MODINDICES(ALL 4);
!savedata: file=infludiagnostics.txt; save is
!cooks;
